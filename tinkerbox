#!/usr/bin/env fish

# SPDX-License-Identifier: MIT
#
# This file is part of the tinkerbox project:
# 	https://codeberg.org/AkhIL/tinkerbox

set -l DEFAULT_IMAGE debian:latest

set -l CMD_NAME (basename (status filename))

set -l MAIN_HELP "\
Tinkerbox is a tool to manage interactive podman containers for experements and development.

Usage: 
	$CMD_NAME [options] <command> [command args]

Options:
	-h, --help	Show this help.

Available commands:
	cr, create	Create a new container.
	en, enter	Enter a container.
	ls, list	!Unimplemented!
	rm, remove	!Unimplemented!

To get command help:
	$CMD_NAME <command> --help
"

set -l CREATE_HELP "\
Usage: 
	$CMD_NAME create [options] <container_name> [ -- [podman args]]

Options:
	-I<image>, --image=<image>	Use provided image to create a new container.
	-B, --share-dbus		Share DBus between the host and a new container.
	-G, --share-gpu			Share GPU between the host and a new container.
	-P, --share-pipewire		Share PipeWire between the host and a new container.
	-A, --share-pulse		Share PulseAudio between the host and a new container.
	-W, --share-wayland		Share Wayland display between the host and a new container.
	-X, --share-x11			Share X11 display between the host and a new container.
	-h, --help			Show this help.

Supported images:
	alpine
	archlinux
	debian (default)
	fedora
	opensuse/tumbleweed

	Other images may work but not tested.
"

set -l ENTER_HELP "\
Usage:
	$CMD_NAME enter [options] <container_name>

Options:
	-R, --root	Enter as the root user.
	-h, --help	Show this help.
"

set -l LIST_HELP "\
TODO
"

set -l REMOVE_HELP "\
TODO
"

set -l BOOTSTRAP_SCRIPT '#!/bin/sh

[ -n "$TINKERBOX_DEBUG" ] && set -x

echo -n "Package manager: "
if command -v apk; then
	apk add sudo shadow
elif command -v apt-get; then
	apt-get update
	apt-get install -y sudo less
elif command -v dnf; then
	dnf install -y sudo less
elif command -v pacman; then
	pacman -Sy sudo less
elif command -v zypper; then
	zypper install -y sudo less
else
	echo WARNING: Unsupported distro!
fi

USERS_HOME=/home/$TINKERBOX_HOST_USER
usermod --home $USERS_HOME $TINKERBOX_HOST_USER
if [ ! -d $USERS_HOME ]; then
	mkdir -p $USERS_HOME
	chown $TINKERBOX_HOST_USER: $USERS_HOME
	chmod 700 $USERS_HOME
fi

USER_SHELL=$(getent passwd $(id -u $TINKERBOX_HOST_USER) | cut -d: -f7)
BASH_PATH=$(command -v bash)
if [ $USER_SHELL = /bin/sh ] && [ -n "$BASH_PATH" ]; then
	usermod --shell $BASH_PATH $TINKERBOX_HOST_USER
fi

mkdir -p /etc/sudoers.d
cat > /etc/sudoers.d/$TINKERBOX_HOST_USER << EOF
$TINKERBOX_HOST_USER ALL=(ALL:ALL) NOPASSWD: ALL
EOF

test -d /mnt/shared && chmod a+rwX /mnt/shared
'

set -l LOGIN_SCRIPT '#!/bin/sh

[ -n "$TINKERBOX_DEBUG" ] && set -x

cd
SHELL=$(getent passwd $(id -u) | cut -d: -f7)
exec $SHELL -l
'

set -l INIT_SCRIPT '#!/bin/sh

[ -n "$TINKERBOX_DEBUG" ] && set -x

# This script is being executed by your user.
# Use sudo to gain root privileges.

echo Starting container

# Spown your daemons here.

# python3 -m http.server -d $HOME 8000 &
# pids="$pids $!"

on_sigterm() {
	echo Caught SIGTERM, exiting...

	# Stop your daemons here.

	# kill $pids
	# wait $pids

	exit
}

# Catch a signal from `podman stop`.
trap "on_sigterm" TERM; sleep infinity & wait;
'

function create \
	--inherit-variable CREATE_HELP \
	--inherit-variable DEFAULT_IMAGE \
	--inherit-variable BOOTSTRAP_SCRIPT \
	--inherit-variable INIT_SCRIPT \
	--inherit-variable LOGIN_SCRIPT

	argparse \
		--name "tinkerbox" \
		--stop-nonopt \
		'I/image=?' \
		'B/share-dbus' \
		'G/share-gpu' \
		'P/share-pipewire' \
		'A/share-pulse' \
		'W/share-wayland' \
		'X/share-x11' \
		'h/help' \
		'd-debug' \
		-- $argv
	or begin
		echo "$CREATE_HELP"
		exit 1
	end

	if set --query _flag_help
		echo $CREATE_HELP
		exit 0
	end

	set -l create_args
	set -l custom_podman_args
	set -l separator_reached false
	for arg in $argv
		if [ "$arg" = "--" ]
			set separator_reached true
		else if $separator_reached
			set --append custom_podman_args $arg
		else
			set --append create_args $arg
		end
	end

	if [ (count $create_args) -ne 1 ]
		echo "$CREATE_HELP"
		exit 1
	end
	set -l box_name $create_args[1]

	set -l image $DEFAULT_IMAGE
	if set --query _flag_image
		set image $_flag_image
	end

	set -l podman_args
	set -l bootstrap_commands

	set --append podman_args container run --detach
	set --append podman_args --name=$box_name
	set --append podman_args --hostname=$box_name
	set --append podman_args --label=manager=tinkerbox

	set --append podman_args --env=TINKERBOX_HOST_USER=$USER
	if set --query _flag_debug
		set --append podman_args --env=TINKERBOX_DEBUG=1
	end

	set --append podman_args --userns=keep-id
	# set --append podman_args --group-add=keep-groups
	
	# Bypass SELinux restrictions on Fedora host
	set --append podman_args --security-opt label=type:container_runtime_t
	
	# TODO: deduplicate volumes
	set --append podman_args --volume=tinkerbox-shared:/mnt/shared
	set --append podman_args --volume={$box_name}-home:/home

	set --append podman_args --mount=type=tmpfs,dst=/run

	if set --query _flag_share_pulse
		set --append podman_args --annotation=ru.akhil.tinkerbox.share_pulse=true
		set --append podman_args --mount=type=bind,src=$XDG_RUNTIME_DIR/pulse,dst=$XDG_RUNTIME_DIR/pulse
	end

	if set --query _flag_share_pipewire
		set --append podman_args --annotation=ru.akhil.tinkerbox.share_pipewire=true
		for socket in $XDG_RUNTIME_DIR/pipewire-?
			set --append podman_args --mount=type=bind,src=$socket,dst=$socket
		end
	end

	if set --query _flag_share_dbus; and set --query XDG_RUNTIME_DIR
		set --append podman_args --annotation=ru.akhil.tinkerbox.share_dbus=true
		set --append podman_args --mount=type=bind,src=$XDG_RUNTIME_DIR/bus,dst=$XDG_RUNTIME_DIR/bus
	end

	if set --query _flag_share_wayland; and set --query XDG_RUNTIME_DIR; and set --query WAYLAND_DISPLAY
		set --append podman_args --annotation=ru.akhil.tinkerbox.share_wayland=true
		set --append podman_args --mount=type=bind,src=$XDG_RUNTIME_DIR/$WAYLAND_DISPLAY,dst=$XDG_RUNTIME_DIR/$WAYLAND_DISPLAY
		set --append podman_args --env=WAYLAND_DISPLAY=$WAYLAND_DISPLAY
	end

	if set --query _flag_share_x11
		set --append podman_args --annotation=ru.akhil.tinkerbox.share_x11=true
		set --append podman_args --mount=type=bind,src=/tmp/.X11-unix,dst=/tmp/.X11-unix
	end

	if set --query _flag_share_gpu
		set --append podman_args --annotation=ru.akhil.tinkerbox.share_gpu=true
		if [ -d /dev/dri ]
			for dev in (find /dev/dri -type c)
				set --append podman_args --device=$dev:$dev:rw
			end
			for link in (find /dev/dri -type l)
				set --append bootstrap_commands "mkdir -p $(path dirname $link)"
				set --append bootstrap_commands "ln -s $(readlink $link) $link"
			end
		end
	end

	for arg in $custom_podman_args
		set --append podman_args $arg
	end

	set --append podman_args $image
	set --append podman_args sh -c 'set -x; test -x /usr/local/bin/tinkerbox-init && exec /usr/local/bin/tinkerbox-init & pid=$!; trap "test x$pid != x && kill -TERM $pid && wait $pid; exit;" TERM; sleep infinity & wait'

	echo \$ podman $podman_args
	set -l container_id (podman $podman_args); or exit $status

	podman exec --user=root $container_id mkdir -p /usr/local/{bin,sbin}; or exit $status
	set -l tmpfile (mktemp)
	chmod +x $tmpfile

	echo $BOOTSTRAP_SCRIPT > $tmpfile
	for cmd in $bootstrap_commands
		echo $cmd >> $tmpfile
	end
	podman cp $tmpfile $container_id:/usr/local/sbin/tinkerbox-bootstrap

	echo $INIT_SCRIPT > $tmpfile
	podman cp $tmpfile $container_id:/usr/local/bin/tinkerbox-init

	echo $LOGIN_SCRIPT > $tmpfile
	podman cp $tmpfile $container_id:/usr/local/bin/tinkerbox-login

	rm $tmpfile

	podman exec --user=root $container_id sh /usr/local/sbin/tinkerbox-bootstrap
	podman stop $container_id
end

function enter \
	--inherit-variable ENTER_HELP

	argparse \
		--name "tinkerbox" \
		--max-args 1 \
		'R/root' \
		'h/help' \
		-- $argv
	or begin
		echo "$ENTER_HELP"
		exit 1
	end

	if set --query _flag_help
		echo "$ENTER_HELP"
		exit 0
	end

	if [ (count $argv) -ne 1 ]
		echo "$ENTER_HELP"
		exit 1
	end
	set -l box_name $argv[1]

	if not [ "tinkerbox" = "$(podman container inspect --format='{{index .Config.Labels "manager"}}' $box_name)" ]
		echo Not a tinkerbox container: $box_name
		exit 1
	end

	if [ "true" = "$(podman container inspect --format='{{.State.Paused}}' $box_name)" ]
		echo Container is paused
		echo "Call `podman unpause $box_name` to unpause this container"
		exit 1
	end

	if not [ "true" = "$(podman container inspect --format='{{.State.Running}}' $box_name)" ]
		echo \$ podman start $box_name
		podman start $box_name; or exit $status
		timeout 10s podman wait --condition=running $box_name > /dev/null
		or begin
			echo Container not running
			exit 1
		end
	end

	set -l podman_args exec -it

	if [ -d "$XDG_RUNTIME_DIR" ]
		set --append podman_args --env=XDG_RUNTIME_DIR=$XDG_RUNTIME_DIR
		podman exec -u root $box_name sh -c "mkdir -p $XDG_RUNTIME_DIR && chown -R $USER: $XDG_RUNTIME_DIR"
	end

	if [ "true" = "$(podman container inspect --format='{{index .Config.Annotations "ru.akhil.tinkerbox.share_dbus"}}' $box_name)" ]
			and [ -n "$DBUS_SESSION_BUS_ADDRESS" ]
		set --append podman_args --env=DBUS_SESSION_BUS_ADDRESS=$DBUS_SESSION_BUS_ADDRESS
	end
		
	if [ "true" = "$(podman container inspect --format='{{index .Config.Annotations "ru.akhil.tinkerbox.share_x11"}}' $box_name)" ]
			and [ -n "$DISPLAY" ]
		set --append podman_args --env=DISPLAY=$DISPLAY
		if [ -n "$XAUTHORITY" ]
			set --append podman_args --env=XAUTHORITY=$XAUTHORITY
			podman exec -u root $box_name sh -c "mkdir -p $(path dirname $XAUTHORITY)"
			podman cp $XAUTHORITY $box_name:$XAUTHORITY
		end
	end

	set --query TERM; and set --append podman_args --env=TERM=$TERM
	set --query COLORTERM; and set --append podman_args --env=COLORTERM=$COLORTERM

	if set --query _flag_root
		set --append podman_args --user=root
	end

	set --append podman_args $box_name
	set --append podman_args sh /usr/local/bin/tinkerbox-login

	echo \$ podman $podman_args
	podman $podman_args
	exit $status
end

function list \
	--inherit-variable LIST_HELP

	echo Not implemented.
	echo Use \`podman containers list\` for now.
	exit 1
end


function remove \
	--inherit-variable REMOVE_HELP

	echo Not implemented.
	echo Use \`podman container rm\` for now.
	exit 1
end

argparse \
	--name "tinkerbox" \
	--stop-nonopt \
	'd-debug' \
	'h/help' \
	-- $argv
or begin
	echo $MAIN_HELP
	exit 1
end

if set --query _flag_debug
	set fish_trace on
end

if set --query _flag_help
	echo $MAIN_HELP
	exit 0
end

switch $argv[1]
	case cr create
		set -l debug_flag
		if set --query _flag_debug
			set debug_flag --debug
		end
		create $debug_flag $argv[2..]
	case en enter
		enter $argv[2..]
	case rm remove
		remove $argv[2..]
	case ls list
		list $argv[2..]
	case *
		echo "$MAIN_HELP"
		exit 1
end

# vim: ts=8 sw=8 noet
