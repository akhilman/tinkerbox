#!/usr/bin/env fish

# SPDX-License-Identifier: MIT
#
# This file is part of the tinkerbox project:
# 	https://codeberg.org/AkhIL/tinkerbox

set -l DEFAULT_IMAGE debian:latest

set -l CMD_NAME (path basename (status filename))

set -l MAIN_HELP "\
Tinkerbox is a tool to manage interactive podman containers for experements and development.

Usage: 
	$CMD_NAME [options] <command> [command args]

Options:
	-h, --help	Show this help.

Available commands:
	cr, create	Create a new container.
	en, enter	Enter a container.
	ls, list	List volumes and containers managed by Tinkerbox.
	rm, remove	Remove containers.

To get command help:
	$CMD_NAME <command> --help
"

set -l CREATE_HELP "\
Usage: 
	$CMD_NAME create [options] <container_name> [ -- [podman args]]

Options:
	-i<image>, --image=<image>	Use provided image to create a new container.
	-e, --enter			Enter into the new container after creating it.
	-b, --share-dbus		Share DBus between the host and a new container.
	-g, --share-gpu			Share GPU between the host and a new container.
	-p, --share-pipewire		Share PipeWire between the host and a new container.
	-a, --share-pulse		Share PulseAudio between the host and a new container.
	-w, --share-wayland		Share Wayland display between the host and a new container.
	-x, --share-x11			Share X11 display between the host and a new container.
	-h, --help			Show this help.

Supported images:
	alpine
	archlinux
	debian (default)
	fedora
	opensuse/tumbleweed

	Other images may work but not tested.
"

set -l ENTER_HELP "\
Usage:
	$CMD_NAME enter [options] <container_name>

Options:
	-r, --root	Enter as the root user.
	-h, --help	Show this help.
"

set -l LIST_HELP "\
Usage:
	$CMD_NAME list [options]

Options:
	-h, --help	Show this help.
"

set -l REMOVE_HELP "\
Usage:
	$CMD_NAME remove [options] [container_name] [container_name]...

Options:
	-s, --stop	Stop running containers.
	-v, --volume	Remove home volumes.
	-h, --help	Show this help.
"

set -l BOOTSTRAP_SCRIPT '#!/bin/sh

[ -n "$TINKERBOX_DEBUG" ] && set -x

echo -n "Package manager: "
if command -v apk; then
	apk add sudo shadow
elif command -v apt-get; then
	apt-get update
	apt-get install -y sudo procps less
elif command -v dnf; then
	dnf install -y sudo procps-ng less
elif command -v pacman; then
	pacman -Sy sudo less
elif command -v zypper; then
	zypper install -y sudo which less
else
	echo WARNING: Unsupported distro!
fi

USER_HOME=/home/%USER%
mkdir -p $USER_HOME
chown %USER%: $USER_HOME
chmod 700 $USER_HOME
usermod --home $USER_HOME %USER%

USER_SHELL=$(getent passwd $(id -u %USER%) | cut -d: -f7)
BASH_PATH=$(command -v bash)
if [ $USER_SHELL = /bin/sh ] && [ -n "$BASH_PATH" ]; then
	usermod --shell $BASH_PATH %USER%
fi

mkdir -p /etc/sudoers.d
cat > /etc/sudoers.d/%USER% << EOF
%USER% ALL=(ALL:ALL) NOPASSWD: ALL
EOF
'

set -l LOGIN_SCRIPT '#!/bin/sh

[ -n "$TINKERBOX_DEBUG" ] && set -x

cd
SHELL=$(getent passwd $(id -u) | cut -d: -f7)
exec $SHELL -l
'

set -l ENTRYPOINT_SCRIPT '#!/bin/sh

[ -n "$TINKERBOX_DEBUG" ] && set -x

# This script will be executed as the root user.

echo Starting container

INIT_USERS="root %USER%"
INIT_SCRIPT=init

for user in $INIT_USERS; do
	home=$(getent passwd $(id -u $user) | cut -d: -f6)
	init=$home/$INIT_SCRIPT
	if [ -x $init ]; then
		sudo -u $user $init &
	fi
done

sleep infinity & sleep_pid=$!

on_sigterm() {
	echo Caught SIGTERM, exiting...
	kill -TERM $sleep_pid
	jobs -p | xargs -r kill -TERM
	wait
}

trap "on_sigterm" TERM
wait
'

function create \
	--inherit-variable CREATE_HELP \
	--inherit-variable CMD_NAME \
	--inherit-variable DEFAULT_IMAGE \
	--inherit-variable BOOTSTRAP_SCRIPT \
	--inherit-variable ENTRYPOINT_SCRIPT \
	--inherit-variable LOGIN_SCRIPT

	argparse \
		--name "tinkerbox" \
		--stop-nonopt \
		'e/enter' \
		'i/image=?' \
		'b/share-dbus' \
		'g/share-gpu' \
		'p/share-pipewire' \
		'a/share-pulse' \
		'w/share-wayland' \
		'x/share-x11' \
		'h/help' \
		'd-debug' \
		-- $argv
	or begin
		echo "$CREATE_HELP"
		exit 1
	end

	if set --query _flag_help
		echo $CREATE_HELP
		exit 0
	end

	set -l create_args
	set -l custom_podman_args
	set -l separator_reached false
	for arg in $argv
		if [ "$arg" = "--" ]
			set separator_reached true
		else if $separator_reached
			set --append custom_podman_args $arg
		else
			set --append create_args $arg
		end
	end

	if [ (count $create_args) -ne 1 ]
		echo "$CREATE_HELP"
		exit 1
	end
	set -l box_name $create_args[1]

	set -l image $DEFAULT_IMAGE
	if set --query _flag_image
		set image $_flag_image
	end

	set -l podman_args
	set -l bootstrap_commands

	if podman volume exists {$box_name}-home
		echo Using existing home volume: {$box_name}-home
	else
		echo -n "Creating new home volume: "
		podman volume create --label=manager=tinkerbox {$box_name}-home
	end

	set --append podman_args container run --detach
	set --append podman_args --name=$box_name
	set --append podman_args --hostname=$box_name
	set --append podman_args --label=manager=tinkerbox
	set --append podman_args --env=CONTAINER_MANAGER=tinkerbox

	if set --query _flag_debug
		set --append podman_args --env=TINKERBOX_DEBUG=1
	end

	set --append podman_args --userns=keep-id
	# set --append podman_args --group-add=keep-groups
	
	# Bypass SELinux restrictions on Fedora host
	set --append podman_args --security-opt label=type:container_runtime_t
	
	set --append podman_args --volume={$box_name}-home:/home
	set --append podman_args --mount=type=tmpfs,dst=/run

	if set --query _flag_share_pulse
		set --append podman_args --annotation=ru.akhil.tinkerbox.share_pulse=true
		set --append podman_args --mount=type=bind,src=$XDG_RUNTIME_DIR/pulse,dst=$XDG_RUNTIME_DIR/pulse
	end

	if set --query _flag_share_pipewire
		set --append podman_args --annotation=ru.akhil.tinkerbox.share_pipewire=true
		for socket in $XDG_RUNTIME_DIR/pipewire-?
			set --append podman_args --mount=type=bind,src=$socket,dst=$socket
		end
	end

	if set --query _flag_share_dbus; and set --query XDG_RUNTIME_DIR
		set --append podman_args --annotation=ru.akhil.tinkerbox.share_dbus=true
		set --append podman_args --mount=type=bind,src=$XDG_RUNTIME_DIR/bus,dst=$XDG_RUNTIME_DIR/bus
	end

	if set --query _flag_share_wayland; and set --query XDG_RUNTIME_DIR; and set --query WAYLAND_DISPLAY
		set --append podman_args --annotation=ru.akhil.tinkerbox.share_wayland=true
		set --append podman_args --mount=type=bind,src=$XDG_RUNTIME_DIR/$WAYLAND_DISPLAY,dst=$XDG_RUNTIME_DIR/$WAYLAND_DISPLAY
		set --append podman_args --env=WAYLAND_DISPLAY=$WAYLAND_DISPLAY
	end

	if set --query _flag_share_x11
		set --append podman_args --annotation=ru.akhil.tinkerbox.share_x11=true
		set --append podman_args --mount=type=bind,src=/tmp/.X11-unix,dst=/tmp/.X11-unix
	end

	if set --query _flag_share_gpu
		set --append podman_args --annotation=ru.akhil.tinkerbox.share_gpu=true
		if [ -d /dev/dri ]
			for dev in (find /dev/dri -type c)
				set --append podman_args --device=$dev:$dev:rw
			end
			for link in (find /dev/dri -type l)
				set --append bootstrap_commands "mkdir -p $(path dirname $link)"
				set --append bootstrap_commands "ln -s $(readlink $link) $link"
			end
		end
	end

	for arg in $custom_podman_args
		set --append podman_args $arg
	end

	set --append podman_args --user=root
	set --append podman_args $image
	set --append podman_args sh -c 'if [ -x /usr/local/sbin/tinkerbox-entrypoint ]; then exec /usr/local/sbin/tinkerbox-entrypoint; else trap "exit" TERM; sleep infinity & wait; fi'

	echo \$ podman $podman_args
	set -l container_id (podman $podman_args); or exit $status

	podman exec --user=root $container_id mkdir -p /usr/local/{bin,sbin}; or exit $status
	set -l tmpfile (mktemp)
	chmod a+rx $tmpfile

	echo $BOOTSTRAP_SCRIPT | string replace '%USER%' $USER > $tmpfile
	for cmd in $bootstrap_commands
		echo $cmd >> $tmpfile
	end
	podman cp $tmpfile $container_id:/usr/local/sbin/tinkerbox-bootstrap

	echo $ENTRYPOINT_SCRIPT | string replace '%USER%' $USER > $tmpfile
	podman cp $tmpfile $container_id:/usr/local/sbin/tinkerbox-entrypoint

	echo $LOGIN_SCRIPT | string replace '%USER%' $USER > $tmpfile
	podman cp $tmpfile $container_id:/usr/local/bin/tinkerbox-login

	rm $tmpfile

	podman exec --user=root $container_id sh /usr/local/sbin/tinkerbox-bootstrap

	if set -q _flag_enter
		enter $box_name
	else
		podman stop $container_id

		echo
		echo "Enter with:"
		echo "	$CMD_NAME enter $box_name"
	end
end

function enter \
	--inherit-variable ENTER_HELP

	argparse \
		--name "tinkerbox" \
		--max-args 1 \
		'r/root' \
		'h/help' \
		-- $argv
	or begin
		echo "$ENTER_HELP"
		exit 1
	end

	if set --query _flag_help
		echo "$ENTER_HELP"
		exit 0
	end

	if [ (count $argv) -ne 1 ]
		echo "$ENTER_HELP"
		exit 1
	end
	set -l container $argv[1]

	if not [ "tinkerbox" = "$(podman container inspect --format='{{index .Config.Labels "manager"}}' $container)" ]
		echo Not a tinkerbox container: $container
		exit 1
	end

	if [ "true" = "$(podman container inspect --format='{{.State.Paused}}' $container)" ]
		echo Container is paused
		echo "Call `podman unpause $container` to unpause this container"
		exit 1
	end

	if not [ "true" = "$(podman container inspect --format='{{.State.Running}}' $container)" ]
		echo \$ podman start $container
		podman start $container; or exit $status
		timeout 10s podman wait --condition=running $container > /dev/null
		or begin
			echo Container not running
			exit 1
		end
	end

	set -l podman_args exec -it

	# Do not intercept Ctrl-P keys
	set --append podman_args --detach-keys=

	if [ -d "$XDG_RUNTIME_DIR" ]
		set --append podman_args --env=XDG_RUNTIME_DIR=$XDG_RUNTIME_DIR
		podman exec -u root $container sh -c "mkdir -p $XDG_RUNTIME_DIR && chown -R $USER: $XDG_RUNTIME_DIR"
	end

	if [ "true" = "$(podman container inspect --format='{{index .Config.Annotations "ru.akhil.tinkerbox.share_dbus"}}' $container)" ]
			and [ -n "$DBUS_SESSION_BUS_ADDRESS" ]
		set --append podman_args --env=DBUS_SESSION_BUS_ADDRESS=$DBUS_SESSION_BUS_ADDRESS
	end
		
	if [ "true" = "$(podman container inspect --format='{{index .Config.Annotations "ru.akhil.tinkerbox.share_x11"}}' $container)" ]
			and [ -n "$DISPLAY" ]
		set --append podman_args --env=DISPLAY=$DISPLAY
		if [ -n "$XAUTHORITY" ]
			set --append podman_args --env=XAUTHORITY=$XAUTHORITY
			podman exec -u root $container sh -c "mkdir -p $(path dirname $XAUTHORITY)"
			podman cp $XAUTHORITY $container:$XAUTHORITY
		end
	end

	set --query TERM; and set --append podman_args --env=TERM=$TERM
	set --query COLORTERM; and set --append podman_args --env=COLORTERM=$COLORTERM

	if set --query _flag_root
		set --append podman_args --user=root
	else
		set --append podman_args --user=$USER
	end

	set --append podman_args $container
	set --append podman_args sh /usr/local/bin/tinkerbox-login

	echo \$ podman $podman_args
	podman $podman_args
	exit $status
end

function list \
	--inherit-variable LIST_HELP

	argparse \
		--name "tinkerbox" \
		--max-args 0 \
		'h/help' \
		-- $argv
	or begin
		echo "$LIST_HELP"
		exit 1
	end

	if set --query _flag_help
		echo "$LIST_HELP"
		exit 0
	end

	echo Volumes:
	podman volume list --filter label=manager=tinkerbox
	or exit $status

	echo

	echo Boxes:
	podman container list -a --filter label=manager=tinkerbox
	or exit $status

	exit 0
end


function remove \
	--inherit-variable REMOVE_HELP

	argparse \
		--name "tinkerbox" \
		's/stop' \
		'v/volume' \
		'h/help' \
		-- $argv
	or begin
		echo "$REMOVE_HELP"
		exit 1
	end

	if set --query _flag_help
		echo "$REMOVE_HELP"
		exit 0
	end

	set -l containers
	for container in $argv
		if not [ "tinkerbox" = "$(podman container inspect --format='{{index .Config.Labels "manager"}}' $container)" ]
			echo Not a tinkerbox container: $container
			exit 1
		end

		if not podman container exists $container
			echo Container \`$container\` not exists
			continue
		end

		set --append containers $container
	end

	if set -q _flag_stop
		for container in $containers
			if [ "true" = "$(podman container inspect --format='{{.State.Running}}' $container)" ]
				echo -n "Stopping container: "
				podman container stop $container
				or exit $status
			end
		end
	end

	for container in $containers
		if [ "true" = "$(podman container inspect --format='{{.State.Running}}' $container)" ]
			echo Container \`$container\` is running. Skipping.
			continue
		end

		set -l box_name (podman container inspect --format='{{.Name}}' $container)
		or exit $status

		echo -n "Removing container: "
		podman container rm $container
		or exit $status

		if set -q _flag_volume
			echo -n "Removing volume: "
			podman volume rm {$box_name}-home
		end
	end

	exit 0
end

argparse \
	--name "tinkerbox" \
	--stop-nonopt \
	'd-debug' \
	'h/help' \
	-- $argv
or begin
	echo $MAIN_HELP
	exit 1
end

if set --query _flag_debug
	set fish_trace on
end

if set --query _flag_help
	echo $MAIN_HELP
	exit 0
end

switch $argv[1]
	case cr create
		set -l debug_flag
		if set --query _flag_debug
			set debug_flag --debug
		end
		create $debug_flag $argv[2..]
	case en enter
		enter $argv[2..]
	case rm remove
		remove $argv[2..]
	case ls list
		list $argv[2..]
	case \*
		echo "$MAIN_HELP"
		exit 1
end

# vim: ts=8 sw=8 noet
